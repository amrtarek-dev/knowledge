Recipe is a collection of tasks written in a combination of Python and shell code.
which are in the layer directory `/recipes-*/*/*.bb` or `/recipes-*/*/*.bbappend`.
These tasks have names like: 
- do_fetch
- do_unpack
- do_patch
- do_configure
- do_compile
- do_install
- do_build (default)
You can list the recipes tasks by:
``` bash
bitbake -c listtasks core-image-minimal
```
And you can execute specific task by call it without `do_` part
``` bash
bitbake -c fetch busybox
```
Or 
``` bash
bitbake -c fetchall core-image-minimal
```
And to clean a built recipe:
``` bash
bitbake -c cleanall busybox
```
### Recipes files
there are 5 types of recipes files:
1. recipes: `.bb` which contains information about building a unit of software (it include the tasks).
2. append: `.bbappend` it appends its instructions to the end of a recipe `.bb` file.
3. include: `.inc` it has the common to several recipes. they using `include`, or `require` which will produce error if the file is not exist.
4. classes: `.bbclass` it has the common but can be inherited and extended by other classes using `inherit` key word (by default all recipes inherited `classes/base.bbclass`)
5. configuration: `.conf` it is for the configuration variables which govern the project's build process. 

For example to make a `Hello world` in the `meta-nova` layer you would create a directory structure like this:
``` bash
meta-nova/recipes-local/helloworld
|-files
| |_ helloworld.c
|_ helloworld_1.0.bb
```
Usually the recipe files are named `<package-name>_version.bb` ,like we can see here in `helloworld_1.0.bb` which contains:
``` bash
DESCRIPTION = "A friendly program that prints Hello World!"
PRIORITY = "optional"
SECTION = "examples"

LICENSE = "GPLv2"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/GPL-2.0;md5=801f80980d171dd6425610833a22dbe6"

SRC_URI = "file://helloworld.c"
S = "${WORKDIR}"

do_compile() {
	${CC} ${CFLAGS} -o helloworld helloworld.c
}

do_install() {
	install -d ${D}${bindir}
	install -m 0755 helloworld ${D}${bindir}
}
```
The location of source code is set by `SRC_URI`, and the `do_compile` and `do_install` tasks will compile and install the binaries because `${D}` expands to the staging area of the target device and `${bindir}` to the default binary directory `/usr/bin/` in the image.

### Recipe license
Every recipe has a license defined by `LICENSE` in the `*.bb` file, it has the license and its checksum in `LIC_FILES_CHKSUM`.

The license file may be part of the package or you can use the standard license text in `meta/files/common-license` like `LICENSE = "GPLv2"`.
``` bash
LICENSE = "GPLv2"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/GPL-2.0;md5=801f80980d171dd6425610833a22dbe6"
```

Commercial licenses are disallowed, but you can enable them by:
`LICENSE_FLAGS = "commercial"`
and in your `conf/local.conf`
`LICENSE_FLAGS_ACCEPTED = "commercial"`

### Build Recipe
To build the recipe only you can use
``` bash
# bitbake <recipe_name>
bitbake helloworld
```
You will found the binaries in `tmp/work/cortexa8hf-vfp-neon-poky-linux-gnueabi/helloworld/`, and an installing package `rpm` in `cortexa8hf_vfp_neon/helloworld-1.0-r0.cortexa8hf_vfp_neon.rpm`

To add the recipe (package) to the image you have to add it to the `conf/local.conf` file or the layer `conf/layer.conf` by
``` bash
IMAGE_INSTALL:append = " helloworld"
```
> the space before the recipe name is mandatory here, because bitbake will append it to the IMAGE_INSTALL variable.

Then build the image by
``` bash
bitbake core-image-minimal
```
You will find it in `tmp/deploy/images/beaglebone/core-image-minimal-beaglebone.tar.bz2` and it is installed in `usr/bin/helloworld`.


## To Append recipe
Make the same recipe directory and add `append` to the recipe file
like:
``` bash
meta-varapps-ha/recipes-homeassistant/python3-homeassistant_2023.11.0.bbappend
```

and add the update inside it like
``` bash
SUMMARY = "Open-source home automation platform running on Python 3"

FILESEXTRAPATHS:prepend := "${THISDIR}/files:"

SRC_URI += "file://homeassistant.service"

do_install:append() {
	install -d -o ${HOMEASSISTANT_USER} -g homeassistant ${D}${HOMEASSISTANT_CONFIG_DIR}
    # Install systemd unit files and set correct config directory
    install -d ${D}${systemd_unitdir}/system
    install -m 0644 ${WORKDIR}/homeassistant.service ${D}${systemd_unitdir}/system
    sed -i -e 's,@HOMEASSISTANT_CONFIG_DIR@,${HOMEASSISTANT_CONFIG_DIR},g' ${D}${systemd_unitdir}/system/homeassistant.service
    sed -i -e 's,@HOMEASSISTANT_USER@,${HOMEASSISTANT_USER},g' ${D}${systemd_unitdir}/system/homeassistant.service
}
```


or implementing patch:
``` bash
# Append to the SRC_URI to include the patch
FILESEXTRAPATHS:prepend := "${THISDIR}/files:"
SRC_URI += "file://0001-Fix-for-AV_FRAME_DATA_SEI_UNREGISTERED-undeclared-issue.patch"
```